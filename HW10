//곰을 그리는 Bear 클래스 생성
class Bear {
  float x;
  float y;
  int d;
  color c1;
  color c2;

  Bear(float x, float y, int d, color c1, color c2) {
    this.x = x;
    this.y = y;
    this.d = d;
    this.c1 = c1;
    this.c2 = c2;
  }

  void display() {
    stroke(c1);
    fill(c1);
    ellipse(x + d * 2.5, y - d * 12.5, d * 20, d * 16.5);  //얼굴
    ellipse(x - d * 3.3, y - d * 16.3, d * 8, d * 7);    //왼쪽 귀
    ellipse(x + 8 * d, y - 17 * d, d * 7, d * 6);    //오른쪽 귀

    fill(255, 255, 255);
    ellipse(x + d * 5.3, y - d * 9.3, d * 4.5, d * 3.5);    //콧잔등 주위 하얀 원
    fill(91, 48, 8);
    circle(x + d * 7.5, y - 11 * d, d + 1);         //오른쪽 눈
    circle(x + d * 2.5, y - d * 10.5, d + 1);         //왼쪽 눈
    stroke(255, 255, 255);    
    ellipse(x + d * 5.2, y - d * 10.2, d + 1, d - 1);     //코
    
    stroke(244, 196, 130);
    fill(c2);
  
    beginShape();                  //왼쪽 귓속 그리기
    vertex(x - d * 6, y - d * 15.5);
    bezierVertex(x - d * 6, y - d * 15.5, x - d * 5.8, y - d * 20, x - d * 3, y - d * 18.5);
    bezierVertex(x - d * 5, y - d * 17, x - d * 5.5, y - d * 15.5, x - d * 6, y - d * 15.5);
    endShape();
    
    beginShape();                  //오른쪽 귓속 그리기
    vertex(x + d * 9, y - d * 19);
    bezierVertex(x + d * 9, y - d * 19, x + d * 10.5, y - d * 20, x + d * 11, y - d * 17);
    bezierVertex(x + d * d, y - d * 18.5, x + d * 9, y - d * 19, x + d * 9, y - d * 19);
    endShape();
  }
}

//기존 Bear 클래스를 상속받아 홍조를 추가한 PinkBear 클래스 생성
class PinkBear extends Bear {
  PinkBear(float x, float y, int d, color c1, color c2) {
    super(x, y, d, c1, c2);
  }

  void display() {
    super.display();
    fill(254, 206, 189);
    ellipse(x + d * 9.7, y - d * 9.8, d * 3.5, d * 3);    //오른쪽 홍조
    ellipse(x + d/2, y - d * 9, d * 3.5, d * 3);    //왼쪽 홍조   
  }
}

Bear[] bears = new Bear[5];

void setup() {
  size(1000, 500);
  for (int i = 0; i < 5; i++) {
    float r1 = random(100, 500);
    float r2 = random(100, 500);
    if (i % 2 == 0) {
      bears[i] = new Bear(r1 * i, 250, 10, color(244, 196, 130), color (216, 140, 61));
    } else {
      bears[i] = new PinkBear(r2 * i, 450, 10, color(216, 140, 61), color(244, 196, 130));
    }
  }
}

void draw() {
  background(255);
  for (int i = 0; i < 5; i++) {
    bears[i].display();
  }
}
